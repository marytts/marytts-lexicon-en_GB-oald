plugins {
    id 'de.dfki.mary.lexicon-compiler' version '0.1.1'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.1.0'

repositories {
    maven {
        url 'https://dl.bintray.com/marytts/marytts'
    }
    ivy {
        url 'http://festvox.org/packed/festival/2.4/'
        layout 'pattern', {
            artifact '[module].[ext]'
        }
    }
    ivy {
        url 'http://www.cstr.ed.ac.uk/downloads/festival/2.4/'
        layout 'pattern', {
            artifact '[module].[ext]'
        }
    }
}

configurations {
    oald
}

dependencies {
    oald group: 'org.festvox', name: 'festlex_OALD', version: '0.4', ext: 'tar.gz'
}

task copyOald(type: Copy) {
    from configurations.oald
    into "$buildDir/oald-packed"
}

task unpackOald(type: Copy) {
    dependsOn copyOald
    from tarTree(file("$copyOald.destinationDir/festlex_OALD-0.4.tar.gz"))
    into "$buildDir/oald"
    include '**/cuvoald710-0.2.scm', '**/oald_extensions.scm', '**/COPYING'
    eachFile {
        it.path = it.name
    }
    includeEmptyDirs = false
}

task processOald {
    dependsOn unpackOald
    ext.destFile = file("$unpackOald.destinationDir/oald.txt")
    outputs.file destFile
    doLast {
        def stressMapping = [
                '1': "'",
                '2': ','
        ]
        def allophoneSetFile = file('modules/en/gb/oald/allophones.en_GB.xml')
        def allophoneSet = marytts.modules.phonemiser.AllophoneSet.getAllophoneSet(allophoneSetFile.newInputStream(), 'oald')
        destFile.withWriter { dest ->
            fileTree(unpackOald.destinationDir).include('*.scm').each { scmFile ->
                scmFile.eachLine('UTF-8') { line ->
                    (line =~ /\( "(\w+)" +(\w+) \( ?([0-2a-z@ ]+?) ?\) .*\)/).each { match, lemma, pos, transcription ->
                        lemma = lemma.replaceAll('_e', 'é').replaceAll('`a', 'à')
                        // TODO: syllabification fails with "@@", remap to something else
                        transcription = transcription.replaceAll('@@', 'Q')
                        def transcriptionMapped = transcription.replaceAll(~/\b(\S+?)([0-2])?\b/, { all, phone, stress ->
                            def mappedStress = stressMapping[stress] ?: ''
                            mappedStress + phone
                        })
                        def transcriptionSyllabified = allophoneSet.syllabify(transcriptionMapped)
                        def transcriptionNoSpaces = transcriptionSyllabified.replaceAll(' ', '')
                        dest.println "$lemma $transcriptionNoSpaces"
                    }
                }
            }
        }
    }
}

compileLexicon {
    dependsOn processOald
    allophonesFile = file('modules/en/gb/oald/allophones.en_GB.xml')
    lexiconFile = processOald.destFile
}

processResources {
    from unpackOald, {
        include 'COPYING'
    }
    eachFile {
        it.path = "marytts/language/$project.locale/oald/$it.name"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
